finish
/post1

! Создание области точек для расчета

rad

RSYS,101

tchk=110                       ! Количество точек пути (11 точек в 10 кругах)
omnom=0                        ! Номер точки пути
pi=aCOS(-1)
Robl=Rvnutr-(Rvnesh-Rvnutr)/9
PATH,step,tchk,10,1, 

! Создание геометрии пути в одной плоскости
! Возможно ТОЛЬКО В ГЛОБАЛЬНОЙ ДЕКАРТОВОЙ СК

*do,i,1,10

Robl=Robl+(Rvnesh-Rvnutr)/9
alfa=-pi/10
zz=0    ! Имеется ввиду плоскость 

*do,j,1,11

omnom=omnom+1
alfa=alfa+pi/10
PPATH,omnom,0,lic+Robl*cos(alfa),Robl*sin(alfa),zz,100,

*enddo
*enddo

! Запись в массив

RSYS,100

AVPRIN,0, , 
!*  
PDEF,SX,S,X,NOAV
/PBC,PATH, ,0   
!*  
AVPRIN,0, , 
!*  
PDEF,SY,S,Y,NOAV 
/PBC,PATH, ,0   
!*  
AVPRIN,0, , 
!*  
PDEF,SXY,S,XY,NOAV   
/PBC,PATH, ,0   
!*  
!*  
PAGET,TRACPOIN,POIN 
PAGET,TRACDATA,TABL 
PAGET,TRACLABL,LABE 
!*  

! Вывод данных из массива и их переосмысление

*DIM,rr,,tchk
*DIM,tt,,tchk
*DIM,ssx,,tchk
*DIM,ssy,,tchk
*DIM,txy,,tchk

*do,i,1,tchk

rr(i)=sqrt((tracdata(i,1)-lic)**2+tracdata(i,2)**2)

*IF,tracdata(i,1)-lic,GT,0,THEN
tt(i)=atan(abs(tracdata(i,2)/(tracdata(i,1)-lic)))
*else
*IF,tracdata(i,2),GT,0,THEN
tt(i)=pi/2+atan(abs((tracdata(i,1)-lic)/tracdata(i,2)))
*else
tt(i)=pi
*endif
*endif

ssx(i)=tracdata(i,5)
ssy(i)=tracdata(i,6)
txy(i)=tracdata(i,7)

*enddo

! Реализация МНК
al=l
L=tchk

! Вектор SIGMA

*DIM,SIGMA,,3*L
*SET,check,1
i=0

*do,j,1,3*L,
*IF,check,EQ,1,THEN
i=i+1
SIGMA(j)=ssx(i)
check=2
*ELSE

*IF,check,EQ,2,THEN
SIGMA(j)=ssy(i)
check=3
*ELSE
SIGMA(j)=txy(i)
check=1
*ENDIF

*ENDIF 
*enddo
! Вектор SIGMA готов

!                                         Тут мы работаем с М


BETTA=0
KINA=0
TNAPRA=0

! Фиксированное количество членов разложения!

M=3   ! Включая вторую моду

! Матрица S

*DIM,S,ARRAY,3*L,M

M=M-1

*SET,check,1
i=0

*do,j,1,3*L,
*IF,check,EQ,1,THEN
i=i+1
*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((2+n/2+(-1)**n)*cos((n/2-1)*tt(i))-(n/2-1)*cos((n/2-3)*tt(i)))
*enddo
n=1
S(j,n+M)=-n/2*(rr(i)**(n/2-1))*((2+n/2-(-1)**n)*sin((n/2-1)*tt(i))-(n/2-1)*sin((n/2-3)*tt(i)))
check=2
*ELSE

*IF,check,EQ,2,THEN
*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((2-n/2-(-1)**n)*cos((n/2-1)*tt(i))+(n/2-1)*cos((n/2-3)*tt(i)))
*enddo
n=1
S(j,n+M)=-n/2*(rr(i)**(n/2-1))*((2-n/2+(-1)**n)*sin((n/2-1)*tt(i))+(n/2-1)*sin((n/2-3)*tt(i)))
check=3
*ELSE

*do,n,1,M,
S(j,n)=n/2*(rr(i)**(n/2-1))*((n/2-1)*sin((n/2-3)*tt(i))-(n/2+(-1)**n)*sin((n/2-1)*tt(i)))
*enddo
n=1
S(j,n+M)=n/2*(rr(i)**(n/2-1))*((n/2-1)*cos((n/2-3)*tt(i))-(n/2-(-1)**n)*cos((n/2-1)*tt(i)))
check=1
*ENDIF

*ENDIF 
*enddo



!          Матрица S готова

M=M+1

!         Задаем матрицу BM
*DIM,BM,ARRAY,M,M

*do,n1,1,M,
*do,n2,1,M,

BM(n1,n2)=0
*do,j,1,3*L
BM(n1,n2)=BM(n1,n2)+S(j,n1)*S(j,n2)
*enddo

*enddo
*enddo
!         Матрица BM готова

!         Задаем вектор С
*DIM,C,,M

*do,n1,1,M,

C(n1)=0
*do,j,1,3*L
C(n1)=C(n1)+S(j,n1)*SIGMA(j)
*enddo

*enddo
!         Вектор С задан


*DIM,AM,,M
*MOPER,AM,BM,SOLV,C

KIN=AM(1)*SQRT(2*pi)     !МПа*корень из мм
TNAPR=AM(2)*4            !МПа
BETTA=TNAPR*SQRT(pi*al)/KIN

KIN2=-AM(3)*SQRT(2*pi)     !МПа*корень из мм

KINA=KIN*SQRT(0.001) !МПа*корень из м
TNAPRA=TNAPR           !МПа

KINA2=KIN2*SQRT(0.001) !МПа*корень из м

K2_K1=KIN2/KIN

! Коэффициент бетта определен


